Program {
  command
    = space* (moveToCommand (space* drawToCommand)*)? space*

  drawToCommand
    = moveToCommand
    | absoluteLineToCommand
    | relativeLineToCommand
    | absoluteVerticalLineToCommand
    | relativeVerticalLineToCommand
    | absoluteHorizontalLineToCommand
    | relativeHorizontalLineToCommand
    | absoluteEllipticalArcCommand
    | relativeEllipticalArcCommand
    | absoluteSmoothQuadraticBezierCurveToCommand
    | relativeSmoothQuadraticBezierCurveToCommand
    | absoluteQuadraticBezierCurveToCommand
    | relativeQuadraticBezierCurveToCommand
    | absoluteSmoothCubicBezierCurveToCommand
    | relativeSmoothCubicBezierCurveToCommand
    | absoluteCubicBezierCurveToCommand
    | relativeCubicBezierCurveToCommand
    | absoluteClosePathToken
    | relativeClosePathToken

  absoluteCubicBezierCurveToCommand
    = absoluteCubicBezierCurveToToken space* coordinatePairTripletSequence
  absoluteCubicBezierCurveToToken = "C"
  relativeCubicBezierCurveToCommand
    = relativeCubicBezierCurveToToken space* coordinatePairTripletSequence
  relativeCubicBezierCurveToToken = "c"

  absoluteSmoothCubicBezierCurveToCommand
    = absoluteSmoothCubicBezierCurveToToken space* coordinatePairTupleSequence
  absoluteSmoothCubicBezierCurveToToken = "S"
  relativeSmoothCubicBezierCurveToCommand
    = relativeSmoothCubicBezierCurveToToken space* coordinatePairTupleSequence
  relativeSmoothCubicBezierCurveToToken = "s"

  absoluteQuadraticBezierCurveToCommand
    = absoluteQuadraticBezierCurveToToken space* coordinatePairTupleSequence
  absoluteQuadraticBezierCurveToToken = "Q"
  relativeQuadraticBezierCurveToCommand
    = relativeQuadraticBezierCurveToToken space* coordinatePairTupleSequence
  relativeQuadraticBezierCurveToToken = "q"

  absoluteSmoothQuadraticBezierCurveToCommand
    = absoluteSmoothQuadraticBezierCurveToToken space* coordinatePairSequence
  absoluteSmoothQuadraticBezierCurveToToken = "T"
  relativeSmoothQuadraticBezierCurveToCommand
    = relativeSmoothQuadraticBezierCurveToToken space* coordinatePairSequence
  relativeSmoothQuadraticBezierCurveToToken = "t"

  absoluteEllipticalArcCommand
    = absoluteEllipticalArcToken space* ellipticalArcArgumentSequence
  absoluteEllipticalArcToken = "A"
  relativeEllipticalArcCommand
    = relativeEllipticalArcToken space* ellipticalArcArgumentSequence
  relativeEllipticalArcToken = "a"

  ellipticalArcArgumentSequence
    = ellipticalArcArgument --single
      | (ellipticalArcArgument coordinateSeparator? ellipticalArcArgumentSequence) -- recursive

  ellipticalArcArgument
    = coordinatePair coordinateSeparator? relativeNumber coordinateSeparator
      flag coordinateSeparator? flag coordinateSeparator? coordinatePair

  absoluteClosePathToken = "Z"
  relativeClosePathToken = "z"

  absoluteLineToCommand
    = absoluteLineToToken space* coordinatePairSequence
  absoluteLineToToken = "L"
  relativeLineToCommand
    = relativeLineToToken space* coordinatePairSequence
  relativeLineToToken = "l"

  absoluteVerticalLineToCommand
    = absoluteVerticalLineToToken space* coordinateSequence
  absoluteVerticalLineToToken = "V"
  relativeVerticalLineToCommand
    = relativeVerticalLineToToken space* coordinateSequence
  relativeVerticalLineToToken = "v"

  absoluteHorizontalLineToCommand
    = absoluteHorizontalLineToToken space* coordinateSequence
  absoluteHorizontalLineToToken = "H"
  relativeHorizontalLineToCommand
    = relativeHorizontalLineToToken space* coordinateSequence
  relativeHorizontalLineToToken = "h"

  moveToCommand = moveToToken space* coordinatePairSequence
  moveToToken = "M" | "m"

  coordinatePairTripletSequence
    = coordinatePairTriplet coordinateSeparator? coordinatePairTripletSequence -- recursive
      | coordinatePairTriplet -- single

  coordinatePairTupleSequence
    = coordinatePairTuple coordinateSeparator? coordinatePairTupleSequence -- recursive
      | coordinatePairTuple -- single

  coordinatePairSequence
    = coordinatePair coordinateSeparator? coordinatePairSequence -- recursive
    | coordinatePair -- single

  coordinateSequence
    = relativeNumber coordinateSeparator? coordinateSequence -- recursive
    | relativeNumber --single

  coordinatePairTriplet = coordinatePair coordinateSeparator? coordinatePair coordinateSeparator? coordinatePair

  coordinatePairTuple = coordinatePair coordinateSeparator? coordinatePair

  coordinatePair = relativeNumber coordinateSeparator relativeNumber

  relativeNumber = sign? number

  coordinateSeparator
    = space+ ","? space* -- space
    | "," space* -- comma

  sign = "-" | "+"
  flag = "0" | "1"

  number
    =  digit* "." digit+ --decimal
    | digit+ --integer
}
